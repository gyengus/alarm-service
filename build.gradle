import org.apache.tools.ant.taskdefs.condition.Os

plugins {
    id 'java'
    id "org.sonarqube" version "2.7"
}

apply plugin: "jacoco"

sourceCompatibility = '1.11'
targetCompatibility = '1.11'

sourceSets {
    main {
        java {
            srcDirs = ['src/main']
        }
        resources {
            srcDirs = ['resources']
        }
    }
    test {
        java {
            srcDirs = ['src/test']
        }
    }
}

repositories {
	mavenCentral()
}

dependencies {
	testImplementation('org.junit.jupiter:junit-jupiter:5.4.2')
}

jar {
  manifest {
    attributes(
      'Class-Path': configurations.compile.collect { it.getName() }.join(' '),
      'Main-Class': 'hu.gyengus.smarthome.alarm.App'
    )
  }
}

test {
    useJUnitPlatform()
}

String runCommandParameter() {
    if (Os.isFamily(Os.FAMILY_WINDOWS)) {
        return 'build\\libs\\alarm-service.jar'
    } else {
        return './build/libs/alarm-service.jar'
    }
}

task run(type: Exec, group: "Custom", description: "Run the applciation") {
  dependsOn jar
  workingDir '.'
  commandLine 'java', '-jar', runCommandParameter()
}

project.tasks["jacocoTestReport"].dependsOn "build"
project.tasks["jacocoTestCoverageVerification"].dependsOn "jacocoTestReport"
project.tasks["sonarqube"].dependsOn "jacocoTestCoverageVerification"

sonarqube {
    properties {
        property "sonar.host.url", "https://sonarcloud.io"
        property "sonar.projectKey", "gyengus_alarm-service"
        property "sonar.organization", "gyengus-github"
        property "sonar.sources", "src/main"
        property "sonar.tests", "src/test"
        property "sonar.junit.reportPaths", "build/test-results/test"
        property "'sonar.coverage.jacoco.xmlReportPaths", "build/reports/jacoco/test"
    }
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled = true
    }
}
