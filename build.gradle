import org.apache.tools.ant.taskdefs.condition.Os

plugins {
	id 'org.springframework.boot' version '2.2.0.M3'
    id 'java'
    id "org.sonarqube" version "2.7"
}

apply plugin: 'io.spring.dependency-management'
apply plugin: "jacoco"

group = 'hu.gyengus.smarthome'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'
targetCompatibility = '11'

repositories {
	mavenCentral()
	maven { url 'https://repo.spring.io/snapshot' }
	maven { url 'https://repo.spring.io/milestone' }
}

ext {
	set('springCloudVersion', "Hoxton.BUILD-SNAPSHOT")
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-mail'
	implementation 'org.springframework.cloud:spring-cloud-gcp-starter-storage'
	implementation 'org.springframework.cloud:spring-cloud-starter-vault-config'
	testImplementation('org.springframework.boot:spring-boot-starter-test') {
		exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
		exclude group: 'junit', module: 'junit'
	}
}

dependencyManagement {
	imports {
		mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
	}
}

jar {
  manifest {
    attributes(
      'Class-Path': configurations.compile.collect { it.getName() }.join(' '),
      'Main-Class': 'hu.gyengus.smarthome.alarm.App'
    )
  }
}

test {
    useJUnitPlatform()
}

String runCommandParameter() {
    if (Os.isFamily(Os.FAMILY_WINDOWS)) {
        return 'build\\libs\\alarm-service.jar'
    } else {
        return './build/libs/alarm-service.jar'
    }
}

task run(type: Exec, group: "Custom", description: "Run the applciation") {
  dependsOn jar
  workingDir '.'
  commandLine 'java', '-jar', runCommandParameter()
}

project.tasks["jacocoTestReport"].dependsOn "build"
project.tasks["jacocoTestCoverageVerification"].dependsOn "jacocoTestReport"
project.tasks["sonarqube"].dependsOn "jacocoTestCoverageVerification"

sonarqube {
    properties {
        property "sonar.host.url", "https://sonarcloud.io"
        property "sonar.projectKey", "gyengus_alarm-service"
        property "sonar.organization", "gyengus-github"
        property "sonar.sources", "src/main"
        property "sonar.tests", "src/test"
        property "sonar.junit.reportPaths", "build/test-results/test"
        property "'sonar.coverage.jacoco.xmlReportPaths", "build/reports/jacoco/test"
    }
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled = true
    }
}
